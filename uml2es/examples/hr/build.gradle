buildscript {
  repositories {
    maven {url 'http://developer.marklogic.com/maven2/'}
  }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'

    // This plugin allows you to create different environments
    // for your gradle deploy. Each environment is represented
    // by a gradle-${env}.properties file
    // See https://github.com/stevesaliman/gradle-properties-plugin
    // specify the env on the command line with:
    // gradle -PenvironmentName=x ...
    id 'net.saliman.properties' version '1.4.6'

    // This gradle plugin extends the ml-gradle plugin with
    // commands that make the Data Hub Framework do its magic
    id 'com.marklogic.ml-data-hub' version '4.1.0'
}

repositories {
    jcenter()
    maven {url 'http://developer.marklogic.com/maven2/'}

    // Needed for mlcp dependencies
    maven { url "http://plugins.gradle.org/m2" }
    maven { url "http://developer.marklogic.com/maven2/" }

    // Needed for hadoop dependencies for mlcp
    maven { url "http://repository.cloudera.com/artifactory/cloudera-repos/" }
}

configurations {
  // This configuration captures the dependencies for running mlcp (Content Pump). This is only needed if you want
  // to run mlcp via Gradle tasks. If you do, using com.marklogic.gradle.task.MlcpTask is a useful starting point, as
  // shown below.  Need to force to use certain version of xml-apis library.
  mlcp {
      resolutionStrategy {
        force "xml-apis:xml-apis:1.4.01"
      }
    }
}

dependencies {
    // this allows you to write custom java code that depends
    // on the Data Hub Framework library
    compile 'com.marklogic:marklogic-data-hub:4.1.0'
    compile 'com.marklogic:marklogic-xcc:9.0.7'

    mlcp "com.marklogic:mlcp:9.0.6"
    mlcp "org.apache.commons:commons-csv:1.2"
    mlcp files("lib")
}

task loadGlobalEmployee(type: com.marklogic.gradle.task.MlcpTask) {
  def dataDir = "${projectDir}";
  def unixDir = dataDir.replace('\\', '/');
  def regexDir = unixDir+"/data/hr/GlobalCorp/employee";
  def regex = '"' + regexDir + ",'',/,''" + '"'

  classpath = configurations.mlcp
  command = "IMPORT"
  host = mlHost
  port = mlStagingPort.toInteger()
  database = mlStagingDbName

  document_type = "json"
  input_file_path =  "data/hr/GlobalCorp/employee/EmployeeTable.csv"
  input_file_type ="delimited_text" 

  output_collections= "Employee,LoadEmployee,input" 
  output_permissions= "rest-reader,read,rest-writer,update" 
  output_uri_replace=regex
  output_uri_prefix = "/hr/employee/global/"
  output_uri_suffix = ".xml"

  transform_module="/data-hub/4/transforms/mlcp-flow-transform.sjs" 
  transform_namespace="http://marklogic.com/data-hub/mlcp-flow-transform" 
  transform_param "entity-name=Employee,flow-name=LoadEmployee"	
}

task loadGlobalSalary(type: com.marklogic.gradle.task.MlcpTask) {
  def dataDir = "${projectDir}";
  def unixDir = dataDir.replace('\\', '/');
  def regexDir = unixDir+"/data/hr/GlobalCorp/employee";
  def regex = '"' + regexDir + ",'',/,''" + '"'

  println regex

  classpath = configurations.mlcp
  command = "IMPORT"
  host = mlHost
  port = mlStagingPort.toInteger()
  database = mlStagingDbName

  document_type = "json"
  input_file_path =  "data/hr/GlobalCorp/employee/SalaryTable.csv"
  input_file_type ="delimited_text" 

  output_collections= "Salary,LoadEmployee,input" 
  output_permissions= "rest-reader,read,rest-writer,update" 
  output_uri_replace=regex
  output_uri_prefix = "/hr/salary/global/"
  output_uri_suffix = ".xml"

  transform_module="/data-hub/4/transforms/mlcp-flow-transform.sjs" 
  transform_namespace="http://marklogic.com/data-hub/mlcp-flow-transform" 
  transform_param "entity-name=Employee,flow-name=LoadEmployee"	
}

task loadGlobalDepartment(type: com.marklogic.gradle.task.MlcpTask) {
  def dataDir = "${projectDir}";
  def unixDir = dataDir.replace('\\', '/');
  def regexDir = unixDir+"/data/hr/GlobalCorp/department";
  def regex = '"' + regexDir + ",'',/,''" + '"'

  classpath = configurations.mlcp
  command = "IMPORT"
  host = mlHost
  port = mlStagingPort.toInteger()
  database = mlStagingDbName

  document_type = "json"
  input_file_path =  "data/hr/GlobalCorp/department"
  input_file_type ="delimited_text" 

  output_collections= "Department,LoadDepartment,input" 
  output_permissions= "rest-reader,read,rest-writer,update" 
  output_uri_replace=regex
  output_uri_prefix = "/hr/department/global/"
  output_uri_suffix = ".xml"

  transform_module="/data-hub/4/transforms/mlcp-flow-transform.sjs" 
  transform_namespace="http://marklogic.com/data-hub/mlcp-flow-transform" 
  transform_param "entity-name=Department,flow-name=LoadDepartment"	
}

task loadAcme(type: com.marklogic.gradle.task.MlcpTask) {
  def dataDir = "${projectDir}";
  def unixDir = dataDir.replace('\\', '/');
  def regexDir = unixDir+"/data/hr/AcmeTech";
  def regex = '"' + regexDir + ",'',/,''" + '"'

  classpath = configurations.mlcp
  command = "IMPORT"
  host = mlHost
  port = mlStagingPort.toInteger()
  database = mlStagingDbName

  document_type = "json"
  input_file_path =  "data/hr/AcmeTech" 
  input_file_type = "documents" 

  output_collections "Employee,LoadEmployee,input" 
  output_permissions "rest-reader,read,rest-writer,update" 
  output_uri_replace = regex 
  output_uri_prefix = "/hr/employee/acme/"

  transform_module="/data-hub/4/transforms/mlcp-flow-transform.sjs" 
  transform_namespace="http://marklogic.com/data-hub/mlcp-flow-transform" 
  transform_param "entity-name=Employee,flow-name=LoadEmployee"	
}

task loadSummaryOrgTriples(type: com.marklogic.gradle.task.MlcpTask) {
  classpath = configurations.mlcp
  command = "IMPORT"
  host = mlHost
  port = mlFinalPort.toInteger()
  database = mlFinalDbName

  document_type = "json"
  input_file_path =  "data/triples/global-org.ttl" 
  input_file_type = "rdf" 
  output_uri_prefix = "/triplestore"
  output_graph = "http://www.w3.org/ns/org"
}

task runInputMLCP() {
  dependsOn 'loadAcme'
  dependsOn 'loadGlobalEmployee'
  dependsOn 'loadGlobalSalary'
  dependsOn 'loadGlobalDepartment'
}

task loadXMI(type: com.marklogic.gradle.task.MlcpTask) {
  def xmiDir = new File("${projectDir}").getParentFile().getAbsolutePath() + "/umlModels"
  def unixDir = xmiDir.replace('\\', '/');
  def regex = '"' + unixDir + ",'',/,''" + '"'

  classpath = configurations.mlcp
  command = "IMPORT"
  document_type = "xml"
  input_file_path = xmiDir + "/DHFEmployeeSample.xml"
  input_file_type = "documents"
  output_uri_replace = regex
  output_uri_prefix = "/xmi2es/xmi/"
  output_permissions = "rest-reader,read,rest-writer,update"
  output_collections = "xmi2es"
  host = mlHost
  port = mlFinalPort.toInteger()

  transform_module = "/xmi2es/xmi2esTransform.xqy"
  transform_namespace ="http://marklogic.com/xmi2es"
  transform_param = "dummy"
}

task loadExtendedModel(type: com.marklogic.gradle.task.MlcpTask) {
  classpath = configurations.mlcp
  command = "IMPORT"
  host = mlHost
  port = mlFinalPort.toInteger()

  input_file_path =  "data/entity-services-extension/*.ttl" 
  input_file_type = "rdf" 
  output_uri_prefix = "/triplestore"
}

task fetchDescriptors(type: com.marklogic.gradle.task.MlcpTask) {
  classpath = configurations.mlcp
  command = "EXPORT"
  output_file_path = "data/entity-services-dump"
  collection_filter = "xmi2es"
  host = mlHost
  port = mlFinalPort.toInteger()
}

task copyES(type: Copy) {
    from "data/entity-services-dump/xmi2es/es/DHFEmployeeSample.json"
    into "data/entity-services" 
}

task copyESExt(type: Copy) {
    from "data/entity-services-dump/xmi2es/extension/DHFEmployeeSample.ttl"
    into "data/entity-services-extension" 
}

task deleteESDump(type: Delete) {
    delete "data/entity-services-dump"
}

task includeXMI2ESTransform(type: Copy) {
    from "../../uml2esTransform/src/main/ml-modules/root/xmi2es"
    into "src/main/ml-modules/root/xmi2es"
}

task ingestModel() {
  dependsOn 'deleteESDump'
  dependsOn 'loadXMI'
  dependsOn 'fetchDescriptors'
  dependsOn 'copyES'
  dependsOn 'copyESExt'
  tasks.findByName('loadXMI').mustRunAfter 'deleteESDump'
  tasks.findByName('fetchDescriptors').mustRunAfter 'loadXMI'
  tasks.findByName('copyES').mustRunAfter 'fetchDescriptors'
  tasks.findByName('copyESExt').mustRunAfter 'fetchDescriptors'
}

// use this instead of the heavy mlGenerateModelArtifacts
task deployESModelToFinal(type: com.marklogic.gradle.task.MlcpTask) {
  def dataDir = "${projectDir}";
  def unixDir = dataDir.replace('\\', '/');
  def regexDir = unixDir+"/data/entity-services";
  def regex = '"' + regexDir + ",'',/,''" + '"'

  classpath = configurations.mlcp
  command = "IMPORT"
  document_type = "json"
  input_file_path = "data/entity-services"
  input_file_type = "documents"
  output_uri_replace = regex
  output_uri_prefix = "/marklogic.com/entity-services/models/"
  output_permissions = "rest-reader,read,rest-writer,update"
  output_collections = "http://marklogic.com/entity-services/models"
  host = mlHost
  port = mlFinalPort.toInteger()
}


/*
This runs the cookie cutter. I have it as a server eval task. The task basically calls SJS in the server-side
to cut the cookies and store them in the Final DB. We then use MLCP import (fetch cut cookie) to get them.
*/
task cutCookie(type: com.marklogic.gradle.task.ServerEvalTask) {
  def model = project.findProperty("model")
  if (model != null) model = '"' + model + '"'
  def entities = project.findProperty("entities") 
  if (entities != null) entities = '"' + entities + '"'
  def entitySelect = project.findProperty("entitySelect") 
  if (entitySelect != null) entitySelect = '"' + entitySelect + '"'
  def dataFormat = project.findProperty("dataFormat") 
  if (dataFormat != null) dataFormat = '"' + dataFormat + '"'
  def pluginFormat = project.findProperty("pluginFormat") 
  if (pluginFormat != null) pluginFormat = '"' + pluginFormat + '"'
  def flowName = project.findProperty("flowName") 
  if (flowName != null) flowName = '"' + flowName + '"'
  def mappingHints = project.findProperty("mappingHints") 
  if (mappingHints != null) mappingHints = '"' + mappingHints + '"'

def js = "'use strict';\n"
js += 'const cookie = require("/xmi2es/cookieCutter.sjs");\n'
js += 'cookie.cutCookie(' + model + ',' + entitySelect + ',' + entities + ',' + dataFormat + ',' + pluginFormat + ',' + flowName + ',' + mappingHints + ');\n'
println js
  javascript = js;
}

task fetchCutCookie(type: com.marklogic.gradle.task.MlcpTask) {
  classpath = configurations.mlcp
  command = "EXPORT"
  output_file_path = "data/cookieCutter-dump"
  collection_filter = "cookieCutter," + project.findProperty("model")
  host = mlHost
  port = mlFinalPort.toInteger()
}

task deleteCutCookie(type: Delete) {
    delete "data/cookieCutter-dump"
}

// TODO - If the entities already exist in plugins and overwrite=false, DONT COPY!!!
task promoteNewCode(type: Copy) {
    from "data/cookieCutter-dump/cookie/" + project.findProperty("model")
    into "plugins/entities" 
}

/* 
This is the cookie cutter for DHF entities.  Parameters:
- model: URI of the ES model in the FINAL DB. Mandatory.
- entities: comma-separated list of model entities to expose as plugins. Optional. If you omit this, the cookie cutter will either use all or infer.
- entitySelect: all, infer. Optional. Default: all. Overrides entitities parameter.
- dataFormat: xml or json. Mandatory.
- pluginFormat: xqy or sjs. Mandatory.
- flowName: The harmonization flow with have the name <flowName>_<pluginFormat>. Optional. Defaults to "Harmonize"<PluginName>
- overwite: true/false. This is local gradle project overwrite. The issue is whether to overwrite plugins in the plugin folder. Optional. Defaults to false.
- mappingHint: TDB
*/
task umlCreateEntities() {
  //dependsOn 'loadHints' // TODO - implement this.
  dependsOn 'deleteCutCookie'
  dependsOn 'cutCookie'
  dependsOn 'fetchCutCookie'
  dependsOn 'promoteNewCode'
  //tasks.findByName('cutCookie').mustRunAfter 'loadHints'
  tasks.findByName('fetchCutCookie').mustRunAfter 'deleteCutCookie'
  tasks.findByName('fetchCutCookie').mustRunAfter 'cutCookie'
  tasks.findByName('promoteNewCode').mustRunAfter 'fetchCutCookie'
}


